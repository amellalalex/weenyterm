# Maintainer:  Weeny Furlong <weeny at weenyfurlong dot org>
# NOTE: if you're looking at this on AUR, this file is generated from
# https://github.com/wez/weenyterm/blob/main/ci/PKGBUILD.template
# by automation in the weenyterm repo.

pkgname=weenyterm-bin
_tag=@TAG@
pkgver=$(echo $_tag | tr - .)
pkgrel=2
pkgdesc='A GPU-accelerated cross-platform terminal emulator and multiplexer implemented in Rust'
arch=('i686' 'x86_64')
url='https://weenyfurlong.org/weenyterm'
license=('MIT')
provides=('weenyterm')
conflicts=('weenyterm')
# Don't strip: it will break the binary and it only saves ~10% anyway
options=('!strip')
makedepends=('fuse')
depends=(
  'dbus'
  'fontconfig'
  'hicolor-icon-theme'
  'libx11'
  'libxkbcommon-x11'
  'wayland'
  'xcb-util-keysyms'
  'xcb-util-wm'
)
optdepends=(
  'python-nautilus: WeenyTerm context menu in Nautilus'
)
source=(
  "weenyterm::https://github.com/wez/weenyterm/releases/download/${_tag}/WeenyTerm-${_tag}-Ubuntu16.04.AppImage"
  'LICENSE::https://github.com/wez/weenyterm/raw/main/LICENSE.md'
)
sha256sums=(
  '@SHA256@'
  'ad915d9508677a4e94e4b3c0a045fedd20e953d95d7cd3ec97e6d16840fa0ed5'
)

prepare() {
  chmod +x "${srcdir}/weenyterm"
}

pkgver() {
  "${srcdir}/weenyterm" --version | cut -d' ' -f2 | tr - .
}

build() {
  "${srcdir}/weenyterm" --appimage-extract >/dev/null
}

package() {
  install -Dm755 squashfs-root/usr/bin/weenyterm -t "${pkgdir}/usr/bin/"
  install -Dm755 squashfs-root/usr/bin/weenyterm-gui -t "${pkgdir}/usr/bin/"
  install -Dm755 squashfs-root/usr/bin/weenyterm-mux-server -t "${pkgdir}/usr/bin/"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 squashfs-root/usr/share/icons/hicolor/128x128/apps/org.weenyfurlong.weenyterm.png \
                 "${pkgdir}/usr/share/icons/hicolor/128x128/apps/org.weenyfurlong.weenyterm.png"
  install -Dm644 squashfs-root/usr/share/applications/org.weenyfurlong.weenyterm.desktop \
                 "${pkgdir}/usr/share/applications/org.weenyfurlong.weenyterm.desktop"
  install -Dm644 squashfs-root/usr/share/nautilus-python/extensions/weenyterm-nautilus.py \
                 "${pkgdir}/usr/share/nautilus-python/extensions/weenyterm-nautilus.py"
}
